#!/usr/bin/env zsh

vars+=(dnscrypt_ipaddr dnscrypt_name dnscrypt_key)

dnscrypt-proxy-conf() {
	rconf=""

	[[ -r $H/run/leases ]] && {
		# load dhcp leases in redis
		for i in ${(f)"$(cat $H/run/leases)"}; do
			rconf+="SET dns-lease-${i[(w)4]} ${i[(w)3]}\n"
			rconf+="SET dns-lease-${i[(w)4]}.dowse.it ${i[(w)3]}\n"

			# and the hosts reverses
			rconf+="SET dns-reverse-${i[(w)3]}. ${i[(w)4]}\n"

		done
	}

	[[ -r /etc/dowse/network ]] && {
		for i in ${(f)"$(cat /etc/dowse/network)"}; do

			print "$i" | grep '^#' > /dev/null
			[[ $? = 0 ]] && continue

			# first word may be a mac address
			print "${i[(w)1]}" | grep '^..:..:..:..:..:..' > /dev/null
			if [[ $? = 0 ]]; then
				mac="${i[(w)1]}"
				hostname="${i[(w)2]}"
				ip4="${i[(w)3]}"
			else
				mac=""
				hostname="${i[(w)1]}"
				ip4="${i[(w)2]}"
			fi
			rconf+="SET dns-lease-${hostname} ${ip4}\n"
			rconf+="SET dns-lease-${hostname}.dowse.it ${ip4}\n"
			func "+/etc/network: $mac $hostname $ip4"
		done
	}

	# load the local hosts known
	rconf+="SET dns-lease-${hostname}          ${address}\n"
	rconf+="SET dns-lease-${hostname}.dowse.it ${address}\n"
	rconf+="SET dns-lease-www.dowse.it         ${address}\n"
	rconf+="SET dns-lease-dowse.it             ${address}\n"
	rconf+="SET dns-lease-gateway    ${wan}\n"
	# load the local hosts reverses
	rconf+="SET dns-reverse-${address}. ${hostname}.dowse.it\n"
	rconf+="SET dns-reverse-${wan}. gateway\n"
	print $rconf | redis dynamic
}

dnscrypt-proxy-exec() {

	args=""
	[[ $* =~ cache ]] && args+=",cache"
	[[ $* =~ debug ]] && args+=",debug"
	[[ $* =~ offline ]] && args+=",offline"

	# grab a random server
	# dnscrypt-roundrobin

	# temporary hard-code dyne.org until roundrobin is fixed
	dnscrypt_name="dyne.org"
	#dnscrypt_ipaddr=198.199.70.248:5553
	#dnscrypt_name=2.dnscrypt-cert.dyne.org
	#dnscrypt_key=162F:051A:BACA:A042:A0C9:7769:91B4:BBA8:FE10:8F72:53CB:5077:A055:9BCF:AD39:73C5

	func "Launching dnscrypt-proxy to listen on port 53"

    if [[ $DEBUG = 1 ]]; then
		DOWSE_DOMAINLIST=`pwd`/src/domain-list/data \
						DOWSE_LAN_ADDRESS_IP4=$address \
						hostname=$hostname \
						domain=$lan \
						launch dnscrypt-proxy -a $address:53 \
						-l $H/log/dnscrypt.log \
						-L $R/share/dnscrypt-resolvers.csv \
						-R "$dnscrypt_name" \
						-X $R/lib/dnscrypt-proxy/dnscrypt_dowse.so${args},debug \
						-m 7 -u $dowse_uid
		# log level informational (7 for debug)
		return $?

    else
		pid=$H/run/dnscrypt-proxy.pid
		DOWSE_DOMAINLIST=$R/domain-list/data \
						DOWSE_LAN_ADDRESS_IP4=$address \
						DOWSE_LAN_NETMASK_IP4=$netmask \
						hostname=$hostname \
						domain=$lan \
						launch dnscrypt-proxy -a $address:53 \
						-p $pid -l $H/log/dnscrypt.log \
						-L $R/share/dnscrypt-resolvers.csv \
						-R "$dnscrypt_name" \
						-X $R/lib/dnscrypt-proxy/dnscrypt_dowse.so${args} \
						-d -m 4 -u $dowse_uid
		# log only warning conditions

    fi

    savepid dnscrypt-proxy $pid
}

dnscrypt-roundrobin() {
	#local servertype=",.,yes,yes" ## DNSSEC + No logs
	#local servertype=",.,yes,no"  ## DNSSEC
	local servertype=",.,no,yes"  ## No logs

	func "Choosing a random available dnscrypt server"

	sed 1q $R/share/dnscrypt-resolvers.csv > $R/share/dnscrypt-resolvers-grepped.csv
	grep $servertype $R/share/dnscrypt-resolvers.csv >> $R/share/dnscrypt-resolvers-grepped.csv

	# prints a random line from the resolvers csv file
	local _rndresolv="$(perl -wne '$a = $_ if rand $. < 1 ; END { print $a }' $R/share/dnscrypt-resolvers-grepped.csv)"

	# check if alive
	eval "${execmap[dnscrypt-proxy]} -L $R/share/dnscrypt-resolvers.csv -R ${_rndresolv[(ws:,:)1]} -t 720 -m 1"

	[[ $? = 0 ]] || dnscrypt-roundrobin

	dnscrypt_name="${_rndresolv[(ws:,:)1]}"

	rm -f $R/share/dnscrypt-resolvers-grepped.csv
}
